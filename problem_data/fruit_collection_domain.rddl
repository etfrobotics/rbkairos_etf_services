domain fruit_collection {
    
    requirements = {
        concurrent,
        reward-deterministic
    };
    
    types {
        location : object;         
        aisle_position : object;   
    };
    
    pvariables {
        
       
        unload_station(aisle_position) : { non-fluent, bool, default = false };
        adjacent(aisle_position, aisle_position) : { non-fluent, bool, default = false };
        reachable_from(location, aisle_position) : { non-fluent, bool, default = false };
        distance(aisle_position, aisle_position) : { non-fluent, real, default = 1.0 };
        
        fruit_weight(location) : { non-fluent, real, default = 1.0 };
        fruit_ripe(location) : { non-fluent, bool, default = true };
        
        GRASP_SUCCESS_PROB : { non-fluent, real, default = 0.9};
        MAX_CAPACITY : { non-fluent, real, default = 100.0};
        CAPACITY_THRESHOLD : { non-fluent, real, default = 90.0};
        MAX_FRUIT_WEIGHT : { non-fluent, real, default = 2.0 };
    
        // robot current position
        robot_at(aisle_position) : { state-fluent, bool, default = false };
        
        // Fruit states
        fruit_at(location) : { state-fluent, bool, default = false };              
        fruit_collected(location) : { state-fluent, bool, default = false };       
        fruit_in_bin(location) : { state-fluent, bool, default = false };          
        fruits_unloaded(location) : { state-fluent, bool, default = false };       
        
        position_visited(aisle_position) : { state-fluent, bool, default = false };
        navigate(aisle_position) : { action-fluent, bool, default = false };
        grasp_fruit(location) : { action-fluent, bool, default = false };
        load_to_bin(location) : { action-fluent, bool, default = false };
        unload : { action-fluent, bool, default = false };
    };
     
    // Conditional Probability Functions 
 
    cpfs {
        robot_at'(?a) = 
            if (navigate(?a)) 
                then true
            else if (exists_{?a2 : aisle_position} [navigate(?a2) ^ (?a2 ~= ?a)])
                then false
            else robot_at(?a);

        fruit_at'(?l) = 
            if (grasp_fruit(?l) ^ 
                exists_{?a : aisle_position} [robot_at(?a) ^ reachable_from(?l, ?a)] ^ 
                fruit_at(?l)) then
                if (Bernoulli(GRASP_SUCCESS_PROB))
                    then false  // Successfully grasped
                    else true   // Failed to grasp
            else fruit_at(?l);

        fruit_collected'(?l) = 
            if (unload ^ fruit_in_bin(?l) ^ 
                exists_{?a : aisle_position} [robot_at(?a) ^ unload_station(?a)])
                then false  
            else if (fruit_at(?l) ^ ~fruit_at'(?l))
                then true   
            else if (fruit_in_bin(?l))
                then true   
            else fruit_collected(?l);
        
       
        fruit_in_bin'(?l) = 
            if (unload ^ exists_{?a : aisle_position} [robot_at(?a) ^ unload_station(?a)])
                then false 
            else if (load_to_bin(?l) ^ 
                     exists_{?a : aisle_position} [robot_at(?a) ^ reachable_from(?l, ?a)] ^ 
                     fruit_collected(?l) ^ ~fruit_in_bin(?l))
                then true   
            else fruit_in_bin(?l);

        // To track which positions have been completed 
        position_visited'(?a) = 
            if (robot_at(?a) ^ 
                exists_{?l : location} [load_to_bin(?l) ^ reachable_from(?l, ?a)])
                then true
            else position_visited(?a);

        fruits_unloaded'(?l) = 
            if (unload ^ fruit_in_bin(?l) ^ 
                exists_{?a : aisle_position} [robot_at(?a) ^ unload_station(?a)])
                then true
            else fruits_unloaded(?l);
    };
  
    reward = 
        (sum_{?l : location} [fruit_collected'(?l) ^ ~fruit_collected(?l) ^ fruit_ripe(?l)]) * 30.0
        + (sum_{?l : location} [fruit_in_bin'(?l) ^ ~fruit_in_bin(?l) ^ fruit_ripe(?l)]) * 10
        + (sum_{?l : location} [fruit_in_bin'(?l)]) * 0.2
        + (sum_{?a : aisle_position} [robot_at'(?a) ^ unload_station(?a) ^
            (forall_{?l : location} [~fruit_at'(?l) | ~fruit_ripe(?l)]) ^
            (forall_{?l : location} [(fruit_collected'(?l) ^ fruit_ripe(?l)) => fruits_unloaded'(?l)])]) * 200.0
        + (sum_{?a : aisle_position} [navigate(?a) ^ unload_station(?a) ^
            ((sum_{?l : location} [fruit_in_bin(?l) * fruit_weight(?l)]) >= CAPACITY_THRESHOLD)]) * 500.0
       
        - (sum_{?a1 : aisle_position, ?a2 : aisle_position} 
           [robot_at(?a1) ^ navigate(?a2) ^ adjacent(?a1, ?a2)] * distance(?a1, ?a2)) * 0.3
        
        - (sum_{?l : location} grasp_fruit(?l)) * 0.05
        - (sum_{?l : location} load_to_bin(?l)) * 0.05
        - unload * 0.05
        
        - (sum_{?a : aisle_position} [robot_at(?a) ^ ~unload_station(?a) ^
            ((sum_{?l : location} [fruit_in_bin(?l) * fruit_weight(?l)]) >= CAPACITY_THRESHOLD) ^
            ~exists_{?a2 : aisle_position} [navigate(?a2) ^ unload_station(?a2)]]) * 100.0
     
        - (sum_{?a1 : aisle_position, ?a2 : aisle_position} 
           [robot_at(?a1) ^ navigate(?a2) ^ adjacent(?a1, ?a2) ^ 
            ~unload_station(?a1) ^ ~unload_station(?a2) ^
            ((sum_{?l : location} [fruit_in_bin(?l) * fruit_weight(?l)]) < CAPACITY_THRESHOLD) ^
            exists_{?l : location} [reachable_from(?l, ?a1) ^ fruit_ripe(?l) ^
                                    ((fruit_at(?l) ^ ~fruit_collected(?l)) | 
                                     (fruit_collected(?l) ^ ~fruit_in_bin(?l)))]]) * 12.0;

  
    // Action preconditions
 
    action-preconditions {
        
        forall_{?a : aisle_position} [
            navigate(?a) => exists_{?a_current : aisle_position} [
                robot_at(?a_current) ^ adjacent(?a_current, ?a)
            ]
        ];
        
        // cannot navigate to non-unload positions when capacity threshold is reached
    forall_{?a : aisle_position} [
        (navigate(?a) ^ ~unload_station(?a)) => 
        ((sum_{?l : location} [fruit_in_bin(?l) * fruit_weight(?l)]) < CAPACITY_THRESHOLD)
    ];
  
        forall_{?a : aisle_position} [
            navigate(?a) => ~robot_at(?a)
        ];
  
        forall_{?a1 : aisle_position, ?a2 : aisle_position} [
    (robot_at(?a1) ^ navigate(?a2) ^ adjacent(?a1, ?a2) ^ ~unload_station(?a1)) => 
    (
        (forall_{?l : location} [
            (reachable_from(?l, ?a1) ^ fruit_ripe(?l)) => 
            (fruit_in_bin(?l) | ~fruit_collected(?l))  // â† CHANGED
        ])
        |
        ((sum_{?l : location} [fruit_in_bin(?l) * fruit_weight(?l)]) >= CAPACITY_THRESHOLD)
    )
];
  
        forall_{?a : aisle_position} [
            (navigate(?a) ^ unload_station(?a)) => 
            exists_{?l : location} [fruit_in_bin(?l)]
        ];
    
        forall_{?a1 : aisle_position, ?a2 : aisle_position} [
            (robot_at(?a1) ^ unload_station(?a1) ^ navigate(?a2) ^ adjacent(?a1, ?a2)) =>
            ~exists_{?l : location} [fruit_in_bin(?l)]
        ];
        
        forall_{?a : aisle_position} [
            navigate(?a) => (~position_visited(?a) | unload_station(?a))
        ];

        forall_{?l : location} [
            grasp_fruit(?l) => (
                exists_{?a : aisle_position} [robot_at(?a) ^ reachable_from(?l, ?a)] ^
                fruit_at(?l) ^ 
                fruit_ripe(?l) ^
                ~fruit_in_bin(?l) ^
                // Check capacity for this fruit
                ((sum_{?l2 : location} [fruit_in_bin(?l2) * fruit_weight(?l2)]) + 
                 fruit_weight(?l) <= MAX_CAPACITY)
            )
        ];
        
        forall_{?l : location} [
            load_to_bin(?l) => (
                exists_{?a : aisle_position} [robot_at(?a) ^ reachable_from(?l, ?a)] ^
                fruit_collected(?l) ^ 
                ~fruit_at(?l) ^
                ~fruit_in_bin(?l)
            )
        ];
        
forall_{?l : location} [
    grasp_fruit(?l) => 
    (
        ~exists_{?l2 : location, ?a : aisle_position} [
            (?l2 ~= ?l) ^ 
            robot_at(?a) ^ 
            reachable_from(?l, ?a) ^ 
            reachable_from(?l2, ?a) ^
            ~fruit_at(?l2) ^           
            fruit_collected(?l2) ^     
            ~fruit_in_bin(?l2)         
        ]
    )
];
        
        forall_{?l : location} [
            load_to_bin(?l) => 
            ((sum_{?l2 : location} [fruit_in_bin(?l2) * fruit_weight(?l2)]) + 
             fruit_weight(?l) <= MAX_CAPACITY)
        ];

        unload => (
            exists_{?a : aisle_position} [robot_at(?a) ^ unload_station(?a)] ^
            exists_{?l : location} [fruit_in_bin(?l)]
        );

        forall_{?a : aisle_position} [
            (robot_at(?a) ^ unload_station(?a) ^ 
             exists_{?l : location} [fruit_in_bin(?l)]) =>
            (
                unload ^ 
                ~exists_{?a2 : aisle_position} [navigate(?a2)] ^
                ~exists_{?l2 : location} [grasp_fruit(?l2)] ^
                ~exists_{?l3 : location} [load_to_bin(?l3)]
            )
        ];

        // action limits

        (sum_{?a : aisle_position} navigate(?a)) <= 1;
        (sum_{?l : location} grasp_fruit(?l)) <= 1;
        (sum_{?l : location} load_to_bin(?l)) <= 1;
    };
}
